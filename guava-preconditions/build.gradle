apply plugin: 'maven'
apply plugin: 'java'

version =  '0.1.0-SNAPSHOT'
group = 'uk.co.ordnancesurvey.external'

// tag::repositories[]
repositories {
    mavenCentral()
}
// end::repositories[]

// tag::jar[]
jar {
    baseName = 'guava-preconditions'
}
// end::jar[]

compileJava {
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
}

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

// tag::dependencies[]
dependencies {
    // only needed for annotations
    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'com.google.errorprone:error_prone_annotations:2.0.4'

    testCompile 'junit:junit:4.11'
}
// end::dependencies[]

// tag::wrapper[]
task wrapper(type: Wrapper) {
    gradleVersion = '2.3.0'
}
// end::wrapper[]

test {
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = false
}

/**
 * Note: to upload the archive the developer must specify the username and password in their gradle
 * properties file:
 *    ~/.gradle/gradle.properties
 *       mavenUser=admin
 *       mavenPassword=myAdminPassword
 *
 * Alternative: it is possible to pass in a property to the build like the following.
 *
 *    ./gradle uploadArchives -PsonatypeUsername=MysticalDeployer -PsonatypePassword=password1
 *
 *    authentication(userName: project.hasProperty("sonatypeUsername") ?
 *              project.sonatypeUsername : null, password: project.hasProperty("sonatypePassword") ?
 *              project.sonatypePassword : null)
 *
 */
uploadArchives {

    repositories.mavenDeployer {
        /* TODO:
        beforeDeployment { MavenDeployment deployment ->
            signing.signPom(deployment)
        }*/

        // Ensure gradle works without properties defined
        ext.mavenUser = project.hasProperty('mavenUser') ? project.getProperty('mavenUser') : 'unspecified'
        ext.mavenPassword = project.hasProperty('mavenPassword') ? project.getProperty('mavenPassword') : 'unspecified'

        repository (url: "http://nexus-ordnancesurvey.rhcloud.com/nexus/content/repositories/releases") {
            authentication (userName: mavenUser, password: mavenPassword)
        }
        snapshotRepository (url: "http://nexus-ordnancesurvey.rhcloud.com/nexus/content/repositories/snapshots") {
            authentication (userName: mavenUser, password: mavenPassword)
        }

        pom.project {
            name 'The Google Guava Preconditions Only'
            packaging 'jar'
            description 'Only the preconditions class and immediate requirements from the the Google Guava project'
            delegate.url 'https://www.mapbox.com/'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            scm {
                delegate.url 'scm:git@github.com:OrdnanceSurvey/guava-preconditions.git'
                connection 'scm:git@github.com:OrdnanceSurvey/guava-preconditions.git'
                developerConnection 'scm:git@github.com:OrdnanceSurvey/guava-preconditions.git'
            }

            developers {
                developer {
                    id 'snodnipper'
                    name 'Snodnipper'
                }
            }
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}
task packageSources(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives packageJavadoc
    archives packageSources
}
